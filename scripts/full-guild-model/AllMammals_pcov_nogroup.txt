data {
  nBeta <- dim(X)
  nP <- dim(dX)
}

model {

# Covariance between detection and mean occupancy
 
rho ~ dunif(-1,1)
var.p <- sigma.p /(1.-pow(rho,2))
  

## priors for occupancy parameters
  alpha.mean <- log(psi.mean) - log(1-psi.mean)
  psi.mean ~ dunif(0,1)
  sigma.occ ~ dunif(0,10)
  tau.occ <- pow(sigma.occ,-2)

### priors for true positive detections
  p.mean ~ dunif(0,1)
  b <- log(p.mean) - log(1-p.mean)
  sigma.p ~ dunif(0,10)
  tau.p <- pow(sigma.p,-2)

### Define prior distributions for occupancy effects
for (a in 1:nBeta[2]){   ### Nbeta is the number of covariates in the model
  mbeta[a] ~ dnorm(0,0.01)  ### mbeta is the average effect for each of the Nbeta covariates (community-level hyper parameter)
  sigbeta[a] ~ dunif(0,10)
  tbeta[a] <- pow(sigbeta[a],-2)  ### tbeta is the amount of variability in each of the community-level hyper-parameters
  for (i in 1:(nspp)) {    
    sbeta[i,a] ~ dnorm(0,tbeta[a])  ### sbeta are the species specific covariate effects
  }
}


#Define prior distributions for detection effects where nP is the number of detection covariates in
#the model, pbeta is the community-level hyper-parameter for each of the nP covariates, tpbeta is
#the amount of variability in each of the community-level hyper-parameters, and spbeta is the
#species-specific covariate effects

for (a in 1:nP[2]){
    pbeta[a] ~ dnorm(0,0.01)
    sigpbeta[a] ~ dunif(0,10)
    tpbeta[a] <- pow(sigpbeta[a],-2)  ### tbeta is the amount of variability in covariate effects among species
    for (i in 1:(nspp)) {    
        spbeta[i,a] ~ dnorm(0,tpbeta[a])  ### sbeta are the species specific covariate effects
  }
}


#Define prior distributions for the occupancy and detection covariates for each species 
  for (i in 1:(nspp)) {    
   #Prior distributions for the occupancy and detection covariates for each species 
    alpha[i] ~ dnorm(alpha.mean, tau.occ)

    mu.p[i] <- b + (rho*sigma.p /sigma.occ)*(alpha[i] - alpha.mean)
    P[i] ~ dnorm(mu.p[i], var.p)
}
  
   #Estimate the occupancy probability (latent Z matrix) for each species at each point
  for (j in 1:n) {
      logit(psi[j]) <- alpha[Species[j]] + inprod(mbeta,X[j,]) + inprod(sbeta[Species[j],],X[j,])
	logit(p[j]) <- P[Species[j]] + inprod(pbeta,dX[j,]) + inprod(spbeta[Species[j],],dX[j,])    	
	Z[j] ~ dbern(psi[j])
    	zp[j] <- p[j]*Z[j]
	    D[j] ~ dbin(zp[j], N[j])
  }
}